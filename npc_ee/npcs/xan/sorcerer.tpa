
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							XAN SORCERER
//__________________________________________________________________________________
//__________________________________________________________________________________


ACTION_FOR_EACH char IN ~xan~ ~xan_~ ~xan4~ ~xan6~ ~o#xan09~ ~o#xan25~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%char%.cre~ BEGIN
	COPY_EXISTING ~%char%.cre~ ~override~
		WRITE_BYTE 0x0234 1 // sets level to 1
		WRITE_BYTE 0x0235 1
		WRITE_BYTE 0x0236 1
		WRITE_BYTE 0x273 19 // sets class
		WRITE_SHORT 0x0024 5
		WRITE_SHORT 0x0026 5
		WRITE_BYTE 0x0066 25
		REMOVE_MEMORIZED_SPELLS
		REMOVE_KNOWN_SPELLS
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~spwi112~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~spwi116~ END
	BUT_ONLY
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
		COPY_EXISTING ~%char%.cre~ ~override~
			LPF DELETE_EFFECT INT_VAR match_opcode = 233 END
		BUT_ONLY
		COPY_EXISTING ~%char%.cre~ ~override~
			SET_BG2_PROFICIENCY 90 1
			SET_BG2_PROFICIENCY 96 1
			SET_BG2_PROFICIENCY 107 1
		BUT_ONLY
	END
  END
END

ACTION_IF FILE_EXISTS_IN_GAME ~sw1h13.itm~ THEN BEGIN
	COPY_EXISTING ~sw1h13.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			READ_BYTE 0x1e mage 
			WRITE_BYTE 0x20 (mage BAND 0b11111011)
			READ_BYTE 0x31 prof
			PATCH_IF prof = 96 BEGIN
				WRITE_SHORT 0x1c 19
				WRITE_BYTE 0x31 90
				READ_LONG 0x54 "valid"
				PATCH_IF (%valid% >= 0) BEGIN
					READ_STRREF 0x54 "desc"
					INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
						REPLACE_TEXTUALLY ~Small Sword~ ~Longsword~
					END
					SAY_EVALUATED 0x54 ~%new_desc%~
				END
			END
		END
	BUT_ONLY
END

ACTION_IF FILE_EXISTS_IN_GAME ~xan.bcs~ BEGIN
	EXTEND_BOTTOM ~xan.bcs~ ~npc_ee/npcs/xan/xan_sorcerer.baf~
END

//__________________________________________________________________________________
