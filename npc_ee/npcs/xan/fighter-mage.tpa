
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							XAN FIGHTER/MAGE (BLADESINGER)
//__________________________________________________________________________________
//__________________________________________________________________________________


ACTION_FOR_EACH char IN ~xan~ ~xan_~ ~xan4~ ~xan6~ ~o#xan09~ ~o#xan25~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%char%.cre~ BEGIN
	COPY_EXISTING ~%char%.cre~ ~override~
		WRITE_BYTE 0x0234 1 // sets level to 1
		WRITE_BYTE 0x0235 1
		WRITE_BYTE 0x0236 1
		WRITE_BYTE 0x273 7 // sets class
		WRITE_SHORT 0x0024 9
		WRITE_SHORT 0x0026 9
		WRITE_BYTE 0x0066 10
	BUT_ONLY
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
		COPY_EXISTING ~%char%.cre~ ~override~
			LPF DELETE_EFFECT INT_VAR match_opcode = 233 END
		BUT_ONLY
		COPY_EXISTING ~%char%.cre~ ~override~
			SET_BG2_PROFICIENCY 90 2
			SET_BG2_PROFICIENCY 104 1
			SET_BG2_PROFICIENCY 113 1
		BUT_ONLY
	END
	ACTION_IF FILE_EXISTS_IN_GAME ~d5_blads.2da~ THEN BEGIN
		COPY_EXISTING ~kitlist.2da~ ~override~
			COUNT_2DA_ROWS 8 "rows"
			FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
				READ_2DA_ENTRY "%index%" 5 8 "clab"
				PATCH_IF ("%clab%" STRING_EQUAL_CASE "d5_blads" = 1) BEGIN
					SET "d5_blads" = "%index%"-1
					SET "rows" = 0
				END
			END
		BUT_ONLY
		COPY_EXISTING ~%char%.cre~ ~override~
			WRITE_SHORT 0x244 0
			WRITE_SHORT 0x246 0x4000 + ~%d5_blads%~ // changes kit
			LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_blad1~ END
		BUT_ONLY
	END
  END
END

ACTION_IF FILE_EXISTS_IN_GAME ~sw1h13.itm~ THEN BEGIN
	COPY_EXISTING ~sw1h13.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			READ_BYTE 0x1f fm 
			WRITE_BYTE 0x1f (fm BAND 0b11011111)
			READ_BYTE 0x31 prof
			PATCH_IF prof = 96 BEGIN
				WRITE_SHORT 0x1c 19
				WRITE_BYTE 0x31 90
				READ_LONG 0x54 "valid"
				PATCH_IF (%valid% >= 0) BEGIN
					READ_STRREF 0x54 "desc"
					INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
						REPLACE_TEXTUALLY ~Small Sword~ ~Longsword~
					END
					SAY_EVALUATED 0x54 ~%new_desc%~
				END
			END
		END
	BUT_ONLY
END
//__________________________________________________________________________________
