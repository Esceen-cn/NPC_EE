
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							XAN BARD
//__________________________________________________________________________________
//__________________________________________________________________________________


ACTION_IF FILE_EXISTS_IN_GAME ~d5_bard.2da~ BEGIN
	COPY_EXISTING ~kitlist.2da~ ~override~
		COUNT_2DA_ROWS 8 "rows"
		FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
			READ_2DA_ENTRY "%index%" 5 8 "clab"
			PATCH_IF ("%clab%" STRING_EQUAL_CASE "d5_bard" = 1) BEGIN
				SET "d5_bard" = "%index%"-1
				SET "rows" = 0
			END
		END
	BUT_ONLY
END

ACTION_FOR_EACH char IN ~xan~ ~xan_~ ~xan4~ ~xan6~ ~o#xan09~ ~o#xan25~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%char%.cre~ BEGIN
	COPY_EXISTING ~%char%.cre~ ~override~
		WRITE_BYTE 0x0234 1 // sets level to 1
		WRITE_BYTE 0x0235 1
		WRITE_BYTE 0x0236 1
		PATCH_IF NOT FILE_EXISTS_IN_GAME ~d5_bard.2da~ BEGIN
		  WRITE_BYTE 0x273 5 // sets class
		END
		PATCH_IF FILE_EXISTS_IN_GAME ~d5_bard.2da~ BEGIN
		  WRITE_BYTE 0x273 13 // sets class
		  WRITE_SHORT 0x244 (%d5_bard% >> 16) & 0xffff
		  WRITE_SHORT 0x246 %d5_bard% & 0xffff
		END		
		WRITE_SHORT 0x0024 8
		WRITE_SHORT 0x0026 8
		WRITE_BYTE 0x0066 25
	BUT_ONLY
	ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
		COPY_EXISTING ~%char%.cre~ ~override~
			LPF DELETE_EFFECT INT_VAR match_opcode = 233 END
		BUT_ONLY
		COPY_EXISTING ~%char%.cre~ ~override~
			SET_BG2_PROFICIENCY 90 1
			SET_BG2_PROFICIENCY 105 1
		BUT_ONLY
	END
  END
END

ACTION_FOR_EACH sword IN ~sw1h13.itm~ ~o#xandb~ ~o#xanimb~ ~o#xanims~ ~o#xanmb~ ~o#xanms~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%sword%.itm~ THEN BEGIN
	COPY_EXISTING ~%sword%.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			PATCH_IF NOT FILE_EXISTS_IN_GAME ~d5__bards.d5~ BEGIN
				READ_BYTE 0x1e bard 
				WRITE_BYTE 0x1e (bard BAND 0b10111111)
			END
			PATCH_IF NOT FILE_EXISTS_IN_GAME ~d5__bards.d5~ BEGIN
				READ_BYTE 0x20 thief
				WRITE_BYTE 0x20 (thief BAND 0b10110101)
			END
			READ_BYTE 0x31 prof
			PATCH_IF prof = 96 BEGIN
				WRITE_SHORT 0x1c 19
				WRITE_BYTE 0x31 90
				READ_LONG 0x54 "valid"
				PATCH_IF (%valid% >= 0) BEGIN
					READ_STRREF 0x54 "desc"
					INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
						REPLACE_TEXTUALLY ~Small Sword~ ~Longsword~
					END
					SAY_EVALUATED 0x54 ~%new_desc%~
				END
			END
		END
	BUT_ONLY
  END
END

/*
ACTION_IF FILE_EXISTS_IN_GAME ~xan.bcs~ BEGIN
	EXTEND_BOTTOM ~xan.bcs~ ~npc_ee/npcs/xan/xan_bard.baf~
END
*/

//__________________________________________________________________________________
