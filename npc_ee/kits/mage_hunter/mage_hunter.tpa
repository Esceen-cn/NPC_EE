

//ADD MAGE HUNTER____________________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN

ADD_KIT ~D5MAGEH~
//___________________________________________________________________________________

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5MAGEH       2 2 2 2 2 2 2 2~
~D5MAGEH       2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   9   9   9   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   15  15  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH  	       0   15  15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5MAGEH           1  0  0  1  0  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5MAGEH           0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~npc_ee/kits/mage_hunter/d5_mageh.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 12~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Ra3~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @70101
SAY @70102
SAY @70103
//___________________________________________________________________________________

END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN

ADD_KIT ~D5MAGEH~
//___________________________________________________________________________________

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5MAGEH       3 3 3 3 3 3 3 3~
~D5MAGEH       3 3 3 3 3 3 3 3 0 4 4 4 3 0 4 4 3 3 4 0 3 3 4 3 0 4 0 3 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   9   9   9   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH          -1   0  -1   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   15  15  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH  	       0   15  15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5MAGEH           1  0  0  1  0  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5MAGEH           0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~npc_ee/kits/mage_hunter/d5_mageh.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 12~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Ra3~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @70101
SAY @70102
SAY @70103
//___________________________________________________________________________________

END

//EE KIT EXTRAS_____________________________________________________________________
// 
ACTION_IF GAME_IS ~bgee bg2ee iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@70104)
		STR_VAR
			kit_name = D5MAGEH
			clswpbon = ~1 0 2~
	END
END
//____________________________________________________________________________________

//REVISED SPELL TABLES_______________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_taspl.d5~ THEN BEGIN
	COPY ~npc_ee/kits/mage_hunter/d5_maghb.2da~ ~override/d5_mageh.2da~
		APPEND_FILE ~npc_ee/kits/mage_hunter/raspl2.txt~
END
//____________________________________________________________________________________

//2DA MODIFICATIONS___________________________________________________________________
ACTION_IF FILE_EXISTS_IN_GAME ~d5_strid.d5~ THEN BEGIN
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~npc_ee/kits/mage_hunter/strirang.txt~
		BUT_ONLY
END
ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~npc_ee/kits/mage_hunter/tracking.txt~
		BUT_ONLY
END
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~d5_apr.d5~) OR (FILE_EXISTS_IN_GAME ~TB#TATT.spl~) THEN BEGIN
		COPY_EXISTING ~d5_mageh.2da~ ~override~
			LPM remove_blank_lines
			APPEND_FILE ~npc_ee/kits/mage_hunter/w1apr.txt~
			BUT_ONLY
	END
END
//____________________________________________________________________________________

//MAGE HUNTER ABILITY FILES___________________________________________________________
//
COPY_EXISTING ~spwi115.spl~ ~override/d5_rasho.spl~
	WRITE_SHORT 0x1c 2
COPY_EXISTING ~spwi223.spl~ ~override/d5_radef.spl~
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 1
COPY_EXISTING ~spwi302.spl~ ~override/d5_rarem.spl~
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 2
COPY_EXISTING ~spwi310.spl~ ~override/d5_ranon.spl~
	WRITE_SHORT 0x1c 2
COPY_EXISTING ~spwi318.spl~ ~override/d5_radfl.spl~
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 2
COPY_EXISTING ~spwi321.spl~ ~override/d5_rathr.spl~
	WRITE_SHORT 0x1c 2
COPY_EXISTING ~spwi513.spl~ ~override/d5_rabre.spl~
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 3
//__________________________________________________________________________________

//______________________HLAS________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~TG#TOUG.spl~ THEN BEGIN
	COPY_EXISTING ~LURa0.2da~ ~override/LURa3.2da~	
END
//__________________________________________________________________________________

